### There are five startup files that zsh will read commands in the following order:

1) /etc/zshenv
2) $ZDOTDIR/.zshenv (always)
3a) /etc/zprofile  [[ -o login ]]
3b) $ZDOTDIR/.zprofile: [[ -o login ]]
4a) /etc/zshrc [[ -o interactive ]]
4b) $ZDOTDIR/.zshrc [[ -o interactive ]]
5a) /etc/zlogin [[ -o login ]]
5b) $ZDOTDIR/.zlogin [[ -o login ]]
6a) /etc/zlogout [[ -o login ]]
6b) $ZDOTDIR/.zlogout [[ -o login ]]

# If ZDOTDIR is not set, then the value of HOME is used; this is the usual case.


# `.zshenv' is sourced on all invocations of the shell, unless the -f option is set. It should contain commands to set the command search path, plus other important environment variables. `.zshenv' should not contain commands that produce output or assume the shell is attached to a tty.
# Since .zshenv is always sourced, it often contains exported variables that should be available to other programs. For example, $PATH, $EDITOR, and $PAGER are often set in .zshenv. Also, you can set $ZDOTDIR in .zshenv to specify an alternative location for the rest of your zsh configuration.


# .zprofile is basically the same as .zlogin except that it's sourced directly before .zshrc is sourced instead of directly after it. According to the zsh documentation, ".zprofile is meant as an alternative to `.zlogin' for ksh fans; the two are not intended to be used together, although this could certainly be done if desired."

# ~/.profile is only loaded when running zsh in sh compatibility mode, otherwise it reads .zprofile, as well as .zshrc for interactive and .zlogin for login shells.

# .profile has the additional property that it's loaded by several other shells as well (e.g. bash, unless it finds something more specific; or ksh) in one way or another, so you can store configuration for multiple shells in this file.

# `.zshrc' is sourced in interactive shells. It should contain commands to set up aliases, functions, options, key bindings, etc.
# .zshrc is for interactive shell configuration. You set options for the interactive shell there with the setopt and unsetopt commands. You can also load shell modules, set your history options, change your prompt, set up zle and completion, et cetera. You also set any variables that are only used in the interactive shell (e.g. $LS_COLORS).

# .zlogin is sourced on the start of a login shell. This file is often used to start X using startx. Some systems start X on boot, so this file is not always very useful.

#.zlogout is sometimes used to clear and reset the terminal.

### Personally, I don't use .zprofile or .zlogin, because all of my customization outside of .zshenv is only applicable to interactive shells.


### if $PATH is getting mangled:
# Zsh on Arch sources /etc/profile – which overwrites and exports PATH – after having sourced ~/.zshenv.

# See: https://wiki.archlinux.org/index.php/Zsh#Configuration_files


### If new zsh shells are slow loading:
# If you find the terminal window blank for a few moments before you see the Last Login: line, you are going to need to clear your log files to see speed improvements. This is still an issue as of OSX Lion and will need to be done every several months. Lame, I know. The command is:

sudo rm -rf /private/var/log/asl/*.asl

# add this to your startup file:

skip_global_compinit=1

# bulk rename

# Replace all spaces in filenames with underscores.
zmv '* *' '$f:gs/ /_'

# Rename all files according to a pattern.
zmv '*.(*).(*).([0-9][0-9])*.mkv' '$1_$2-$3.mkv'

# Rename file.JPG to file.jpg
zmv '(*).JPG' '$1.jpg'

# or just
zmv '(*).*' '$1.jpg'

# A plain old glob
print -l *.txt
print -l **/*.txt

# Show text files that end in a number from 1 to 10
print -l **/*<1-10>.txt

# Show text files that start with the letter a
print -l **/[a]*.txt

# Show text files that start with either ab or bc
print -l **/(ab|bc)*.txt

# Show text files that don't start with a lower or uppercase c
print -l **/[^cC]*.txt

# Show only directories
print -l **/*(/)

# Show only regular files
print -l **/*(.)

# Show empty files
print -l **/*(L0)

# Show files greater than 3 KB
print -l **/*(Lk+3)

# Show files modified in the last hour
print -l **/*(mh-1)

# Sort files from most to least recently modified and show the last 3
print -l **/*(om[1,3])

# `.` show files, `Lm-2` smaller than 2MB, `mh-1` modified in last hour,
# `om` sort by modification date, `[1,3]` only first 3 files
print -l **/*(.Lm-2mh-1om[1,3])

# Show every directory that contain directory `.git`
print -l **/*(e:'[[ -d $REPLY/.git ]]':)

# Return the file name (t stands for tail)
print -l *.txt(:t)

# Return the file name without the extension (r stands for remove_extension)
print -l *.txt(:t:r)

# Return the extension
print -l *.txt(:e)

# Return the parent folder of the file (h stands for head)
print -l *.txt(:h)

# Return the parent folder of the parent
print -l *.txt(:h:h)

# Return the parent folder of the first file
print -l *.txt([1]:h)

# Parameter expansion
files=(*.txt)          # store a glob in a variable
print -l $files
print -l $files(:h)    # this is the syntax we saw before
print -l ${files:h}
print -l ${files(:h)}  # don't mix the two, or you'll get an error
print -l ${files:u}    # the :u modifier makes the text uppercase

# :s modifier
variable="path/aaabcd"
echo ${variable:s/bc/BC/}    # path/aaaBCd
echo ${variable:s_bc_BC_}    # path/aaaBCd
echo ${variable:s/\//./}     # path.aaabcd (escaping the slash \/)
echo ${variable:s_/_._}      # path.aaabcd (slightly more readable)
echo ${variable:s/a/A/}      # pAth/aaabcd (only first A is replaced)
echo ${variable:gs/a/A/}     # pAth/AAAbcd (all A is replaced)

# Split the file name at each underscore
echo ${(s._.)file}

# Join expansion flag, opposite of the split flag.
array=(a b c d)
echo ${(j.-.)array} # a-b-c-d

# Short if
if [[ ... ]] command
if [[ ... ]] { command ... }

# Short for
for i ( word ... ) command
for i ( word ... ) { command ... }
for i in word ... ; command

# Short while/until
while [[ ... ]] { command ... }
until [[ ... ]] { command ... }

# Use output of command, when using pipe is not possible
<( command )

# Similar to <( ), but creates temporary file (instead of FD or FIFO), when
# program needs to seek in output.
=( command )
